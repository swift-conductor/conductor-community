import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:10.0.0'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.7.18'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.+'
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
    id 'application'
    id 'jacoco'

    id 'org.sonarqube' version '3.4.0.2513'

    id 'com.netflix.nebula.contacts' version '7.0.1'
    id 'com.netflix.nebula.netflixoss' version '11.3.2'
}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

apply from: "$rootDir/dependencies.gradle"

allprojects {
    apply plugin: 'com.netflix.nebula.netflixoss'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'project-report'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    group = 'com.swiftconductor.conductor'

    configurations.all {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    repositories {
        mavenCentral()

        // repository for -SNAPSHOT.* verions:
        maven {
            url "https://s01.oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencyManagement {
        imports {
            // dependency versions for the BOM can be found at https://docs.spring.io/spring-boot/docs/2.7.3/reference/htmlsingle/#appendix.dependency-versions
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    dependencies {
        implementation('org.apache.logging.log4j:log4j-core')
        implementation('org.apache.logging.log4j:log4j-api')
        implementation('org.apache.logging.log4j:log4j-slf4j-impl')
        implementation('org.apache.logging.log4j:log4j-jul')
        implementation('org.apache.logging.log4j:log4j-web')
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation('org.springframework.boot:spring-boot-starter-log4j2')
        testImplementation 'junit:junit'
        testImplementation "org.junit.vintage:junit-vintage-engine"
    }

    // processes additional configuration metadata json file as described here
    // https://docs.spring.io/spring-boot/docs/2.3.1.RELEASE/reference/html/appendix-configuration-metadata.html#configuration-metadata-additional-metadata
    compileJava.inputs.files(processResources)

    test {
        useJUnitPlatform()
        testLogging {
            events = ["SKIPPED", "FAILED"]
            exceptionFormat = "full"
            displayGranularity = 1
            showStandardStreams = false
        }
    }

}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

task server {
    dependsOn ':conductor-community-server:bootRun'
}

sonarqube {
    properties {
        property "sonar.projectKey", "swift-conductor_conductor"
        property "sonar.organization", "swift-conductor"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

configure(allprojects) {
    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            googleJavaFormat().aosp()
            removeUnusedImports()
            importOrder('java', 'javax', 'org', 'com.swiftconductor', '', '\\#com.swiftconductor', '\\#')
            licenseHeaderFile("$rootDir/licenseheader.txt")
        }
    }
}

configure(allprojects) {
    // see: 
    // https://plugins.gradle.org/plugin/com.netflix.nebula.contacts
    // https://github.com/nebula-plugins/gradle-contacts-plugin
    apply plugin: 'com.netflix.nebula.contacts'

    contacts {
        'hello@swiftsoftwaregroup.com' {
            github 'swift-conductor'
            moniker 'Swift Software Group'
        }
    }
}